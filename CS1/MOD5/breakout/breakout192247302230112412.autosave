////////////////////////
// Global Variables
////////////////////////
final int brickCount = 80;

Brick[] brickSet = new Brick[80];
Ball ball = new Ball();
Paddle paddle = new Paddle();
Game game = new Game();

final color redRow = color(175, 75, 75);
final color orangeRow = color(175, 125, 75);
final color yellowRow = color(175, 175, 75);
final color greenRow = color (75, 175, 75);

final String instructions1 = "Controlls: Left / Right arrow";
final String instructions2 = "Press Space to Shoot";

////////////////////////
// Setup
////////////////////////

void setup() {
    background(175);
    size(1000, 800);

    newBricks();
}

void newBricks() {
    int bWidth = width/10;
    int bHeight = height/20;
    for (int count = 0; count < 80; count++) {
        int x = (count % 10) * bWidth + bWidth / 2;
        int y = (count / 10) * bHeight + bHeight / 2;
        color bColour = brickColour(y);
        brickSet[count] = new Brick(x, y, bWidth, bHeight, bColour);
    }
}

void draw() {
    background(175);
    for (int i = 0; i < 80; i++) {
        brickSet[i].brickDraw();
    }
    
    paddle.padDraw();
    ball.ballDraw();
    game.draw();
    if (game.isPaused == false) {
        ball.ballMove();
        paddle.move();
    }
}

color brickColour(int pos) {
    if (pos < height/20*2) {
        return redRow;
    } else if (pos < height/20*4 && pos > height/20*2) {
        return orangeRow;
    } else if (pos < height/20*6 && pos > height/20*4) {
        return yellowRow;
    } else {
        return greenRow;
    }
}

class Brick {
    color brickColour;
    int brickX, brickY, brickWidth, brickHeight, brickL, brickR, brickT, brickB;
    boolean isNotDestroyed;

    Brick(int x, int y, int w, int h, color colour) {
        brickWidth = w;
        brickHeight = h;
        brickX = x;
        brickY = y;
        brickL = brickX - w/2;
        brickR = brickX + w/2;
        brickT = brickY - h/2;
        brickB = brickY + h/2;

        brickColour = colour;
        isNotDestroyed = true;
    }
    
    void brickDraw() {
        if (isNotDestroyed == true) {

            rectMode(CENTER);
            fill(brickColour);

            rect(brickX, brickY, brickWidth, brickHeight, 5);
        }
    }

    void destroy(){
        isNotDestroyed = false;
        game.bricksDestroyed += 1;
        if (game.bricksDestroyed == 4 || game.bricksDestroyed == 12) game.ballSpeedMultiplier +=1;


        if (brickColour == greenRow) {
            game.points += 10;
        } else if (brickColour == yellowRow) {
            game.points += 30;
        } else if (brickColour == orangeRow) {
            game.points += 50;
            if (game.orangeHit == false) {
                game.ballSpeedMultiplier +=1;
                game.orangeHit = true;
            }
        } else if (brickColour == redRow) {
            game.points += 70;
        }
    }
}

class Ball {
    int ballX, ballY, ballSize, ballXSpeed, ballYSpeed, ballL, ballR, ballT, ballB;

    Ball() {
        ballX = 500;
        ballY = 670;
        ballSize = 30;
        ballXSpeed = 2;
        ballYSpeed = 2;        
    }

    void ballDraw() {
        ballL = ballX - ballSize/2;
        ballR = ballX + ballSize/2;
        ballT = ballY - ballSize/2;
        ballB = ballY + ballSize/2;
        fill(255);
        stroke(1);

        circle(ballX, ballY, ballSize);
    }

    void ballMove() {
        if (ballL <= 0 || ballR >= width) ballXSpeed = -ballXSpeed;
        if (ballY <= 0) {
            ballYSpeed = -ballYSpeed;
        } else if (paddle.hit(ballB, ballX)) {
            ballYSpeed = -ballYSpeed;
            ballXSpeed = paddle.hitLocation(ballX);
        }

        // } || paddle.hit(ballB, ballX)) ballYSpeed = -ballYSpeed;

        for (int i = 0; i < brickCount; i++) {
            if (brickSet[i].isNotDestroyed) {            
                if (leftCheck(i) && rightCheck(i) && topCheck(i) && bottomCheck(i)) {
                    brickSet[i].destroy();
                    ballOffBrick(i);
                }
            }
        }
        ballX = ballX + ballXSpeed * game.ballSpeedMultiplier/2;
        ballY = ballY + ballYSpeed * game.ballSpeedMultiplier/2;
    }

    // booleans checks are named for the ball edge

    boolean leftCheck(int brickNum) {
        return ballL <= brickSet[brickNum].brickR;
    }
    boolean rightCheck(int brickNum) {
        return ballR >= brickSet[brickNum].brickL;
    }
    boolean topCheck(int brickNum) {
        return ballT <= brickSet[brickNum].brickB;
    }
    boolean bottomCheck(int brickNum) {
        return ballB >= brickSet[brickNum].brickT;
    }

    void ballOffBrick(int brickNum) {
        int brickX = brickSet[brickNum].brickX;
        int brickY = brickSet[brickNum].brickY;

        if (dist(ballX, ballY, brickX, brickY) > 50) {
            ballXSpeed = -ballXSpeed;
        } else {
            ballYSpeed = -ballYSpeed;
        }
    }
}

class Paddle {
    int padX, padY, padWidth, padHeight, padT, padL, padR;
    boolean moveLeft, moveRight;

    Paddle() {
        padX = 500;
        padY = 700;
        padHeight = 30;
        padWidth = 200;
        moveLeft = false;
        moveRight = false;
    }

    void padDraw() {
        padT = padY - padHeight/2;
        padR = padX + 120;
        padL = padX - 120;

        fill(255);
        stroke(1);

        rect(padX, padY, padWidth, padHeight, 90);
    }

    void move() {
        if (moveLeft == true &&
            padX >= 120) {
            padX = padX - 20;
        } else if (moveRight == true &&
            padX <= width - 120) {
            padX = padX + 20;
        }
    }

    boolean hit(int ballB, int ballX){
        return (ballB >= padT &&
                ballX <= padR &&
                ballX >= padL);
    }

    int hitLocation(int ballX) {
        int padSection = padWidth/5;
        if (ballX > padX - padSection*5 && ballX < padX - padSection*4){
            return -10;
        } else if (ballX > padX - padSection*4 && ballX < padX - padSection*3) {
            return -8;
        } else if (ballX > padX - padSection*3 && ballX < padX - padSection*2) {
            return -6;
        } else if (ballX > padX - padSection*2 && ballX < padX - padSection) {
            return -4;
        } else if (ballX > padX - padSection && ballX < padX){
            return -2;
        } else if (ballX == padX) {
            return 0;
        } else if (ballX > padX && ballX < padX + padSection) {
            return 2;
        } else if (ballX > padX + padSection && ballX < padX + padSection*2) {
            return 4;
        } else if (ballX > padX + padSection*2 && ballX < padX + padSection*3) {
            return 6;
        } else if (ballX > padX + padSection*3 && ballX < padX + padSection*4) {
            return 8;
        } else if (ballX > padX + padSection*4 && ballX < padX + padSection*5) {
            return 10;
        } else {
            return 2;
        }
    }
}

void keyPressed() {
    if (game.isPaused == false) {
        switch (key) {
            case 'f' :
                paddle.moveRight = true;
            break;
            case 'e' :
                paddle.moveLeft = true;
            break;
        }
    } else {
        switch (key) {
            case 'g' :
                game.isPaused = false;
            break;
        }
    }
}

void keyReleased() {
    if (game.isPaused == false) {
        switch(key) {
            case 'f' :
                paddle.moveRight = false;
            break;
            case 'e' :
                paddle.moveLeft = false;
            break;
        }
    }
}

class Game {
    int lives, points, ballXSpeed, ballYSpeed, bricksDestroyed, ballSpeedMultiplier;
    boolean isPaused, isWon, gameOver, orangeHit;

    Game(){
        lives = 3;
        points = 0;
        bricksDestroyed = 0;
        ballSpeedMultiplier = 2;

        isPaused = true;
        isWon = false;
        gameOver = false;
        orangeHit = false;
    }

    void addPoints(int newPoints){
        points = points + newPoints;
    }

    void draw() {
        drawPoints();
        if (isPaused == true) pauseScreen();
    }

    void drawPoints() {
        fill(255);
        textSize(24);
        textAlign(CORNERS);

        text("SCORE: "+ points, 20, height - 50);
    }

    void pauseScreen() {
        rectMode(CORNERS);
        fill(75, 200);

        rect(0,0, width, height);

        fill(200, 75);
        textSize(64);
        textAlign(CENTER, CENTER);
        text(instructions1, width/2, height/3);
        text(instructions2, width/2, height/3 + 100);
    }
}
